apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

dependencies {
    //Redsys libs
    compile 'com.sun.xml.ws:jaxws-rt:2.2.10'
    compile 'commons-discovery:commons-discovery:0.5'
    compile 'axis:axis-wsdl4j:1.5.1'
    compile 'commons-codec:commons-codec:1.10'
    compile 'org.json:json:20160212'
    compile 'org.bouncycastle:bcprov-jdk15on:1.54'

    // Junit
    testCompile 'junit:junit:4.12'

    // Mockito
    testCompile 'org.mockito:mockito-core:2.18.3'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'

    testCompile 'com.owlike:genson:1.3'
    // https://mvnrepository.com/artifact/javax.ws.rs/javax.ws.rs-api
    testCompile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'

    //Embedded Glassfish
    testCompile 'org.glassfish.embedded:maven-embedded-glassfish-plugin:4.1.1'
    testCompile 'org.glassfish.main.common:simple-glassfish-api:4.1.1'
    testCompile 'org.glassfish.main.extras:glassfish-embedded-all:4.1.1'
    testCompile group: 'org.reflections', name: 'reflections', version: '0.9.11'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    tasks.javadoc.failOnError = false
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'easyredsys-client'
                packaging 'jar'
                description "Librería que facilita la integración con la pasarela de pago Redsys para aplicaciones Java"
                url "https://www.miguelangeljulvez.com/easyredsys"

                scm {
                    url "https://github.com/majulvez/easyredsys"
                    connection "scm:git@github.com:majulvez/easyredsys.git"
                    developerConnection "scm:git@github.com:majulvez/easyredsys.git"
                }

                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "majulvez"
                        name "Miguel Ángel Júlvez"
                    }
                }
            }
        }
    }
}